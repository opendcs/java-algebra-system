plugins {
    id 'java'
    id 'application'
    id 'java-library'
    id 'org.openjfx.javafxplugin' version '0.0.13'
    id 'maven-publish'
    id 'signing'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(11)
    }
    withJavadocJar()
    withSourcesJar()
}

group = "org.opendcs"
archivesBaseName = "java-algebra-system"
version = "0.0.1-SNAPSHOT"
ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

application {
    mainClass = 'CAS'
}

javafx {
    modules = [ 'javafx.controls', 'javafx.fxml' ]
}

repositories {
    mavenCentral()
}

tasks.withType(Javadoc) {
    options.addStringOption('Xdoclint:none','-quiet')
    failOnError false
}

dependencies {
    //implementation 'org.openjfx:javafx:11'
    implementation 'org.processing:core:3.3.7'

    testImplementation 'org.mockito:mockito-core:3.4.6'
    testImplementation 'org.mockito:mockito-junit-jupiter:3.4.6'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.6.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.9.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.9.0'
}

test {
    useJUnitPlatform()
}


publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'java-algebra-system'
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'Java Algebra System'
                description = 'Library for computer algebra work.'
                url = 'https://github.com/opendcs/java-algebra-system'
                scm {
                    connection = 'scm:git:https://github.com/opendcs/java-algebra-system'
                    developerConnection = 'scm:git:git@github.com:opendcs/java-algebra-system.git'
                    url = 'https://github.com/opendcs/java-algebra-system'
                }

                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://opensource.org/license/mit/'
                    }
                }

                developers {
                    developer {
                        id = 'jiachenren'
                        name = 'Jiachen Ren'
                        email = 'https://github.com/JiachenRen'
                    }
                    developer {
                        id = 'opendcs-team'
                        name = 'OpenDCS Dev Team'
                        email = 'https://github.com/opendcs' 
                    }
                }
            }
        }
    }
    repositories {
        maven {
            // change URLs to point to your repos, e.g. http://my.org/repo
            def releasesRepoUrl = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username = project.findProperty("ossrhUsername")
                password = project.findProperty("ossrhPassword")
            }
        }
    }
}

signing {
    required { isReleaseVersion && gradle.taskGraph.hasTask("publish") }
    useGpgCmd()
    sign publishing.publications.mavenJava
}

/*
uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { deployment -> signing.signPom(deployment) }

            repository(url: "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://s01.oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'OpenDCS Java Algebra System'
                packaging 'jar'
                // optionally artifactId can be defined here
                description 'A version of the Java Algebra System packaged by the OpenDCS Team.'
                url 'github.com/opendcs/java-algebra-system'

                scm {
                    connection 'scm:git:https://github.com/opendcs/java-algebra-system'
                    developerConnection 'scm:git:ssh://github.com:opendcs/opendcs.git'
                    url 'https://github.com/opendcs/java-algebra-system/main'
                }

                licenses {
                license {
                    name 'MIT License'
                    url 'https://opensource.org/license/mit/'
                }
                }

                developers {
                    developer {
                        id 'jiachenren'
                        name 'Jiachen Ren'
                        url 'https://github.com/jiachenren'

                    }
                    developer {
                        id 'opendcs'
                        name 'OpenDCS Team'
                        organization 'OpenDCS Consortium'
                        email 'opendcs@freelists.org'
                        organizationurl 'https://github.com/opendcs'
                    }
                }
            }
        }
    }
}*/
